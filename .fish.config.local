# save all env variables in this file
#
source ~/.dotfiles-local/.env_variables.fish

# prompt
#
function fish_prompt
    set_color brblack
    echo -n "["(date "+%H:%M")"] "
    set_color brblack
    echo -n (hostname)
    if [ $PWD != $HOME ]
        set_color brblack
        echo -n ': '
        set_color brblue
        echo -n (basename $PWD)
    end
    set_color green
    printf '%s ' (__fish_git_prompt)
    set_color brblue
    echo -n '| '
    set_color normal
end

# vim mode for fish pretier prompt
#
function fish_mode_prompt --description 'Displays the current mode'
    # Do nothing if not in vi mode
    if test "$fish_key_bindings" = "fish_vi_key_bindings"
        switch $fish_bind_mode
            case default
                set_color --bold red
                echo üÖΩ
            case insert
                set_color --bold green
                echo üÖ∏
            case replace-one
                set_color --bold green
                echo üÜÅ
            case visual
                set_color --bold brmagenta
                echo üÜÖ
        end
        set_color normal
        printf " "
    end
end

# http_proxy port
set -x proxy_port 1087

# use this function to set http_proxy environment variables
#
function set_proxy
    # Use -g to make environment variable outside this function
    set -gx http_proxy  "http://$argv:$proxy_port"
    set -gx https_proxy "http://$argv:$proxy_port"
    set -gx HTTP_PROXY  "http://$argv:$proxy_port"
    set -gx HTTPS_PROXY "http://$argv:$proxy_port"
end

# remove http_proxy environment variables
#
function remove_proxy
    set -e http_proxy
    set -e https_proxy
    set -e HTTP_PROXY
    set -e HTTPS_PROXY
end

# list http_proxy environment variables
#
function ls_proxy
    echo "List http proxys"
    echo "http_proxy: $http_proxy"
    echo "https_proxy: $https_proxy"
    echo "HTTP_PROXY: $HTTP_PROXY"
    echo "HTTPS_PROXY: $HTTPS_PROXY"
end

# list all users and their uids
#
function list_uids
    cut -d: -f1,3 /etc/passwd
end
